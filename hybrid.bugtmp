model{
	# Dispersion
	repShape ~ dgamma(shapeH, shapeH)
	incShape ~  dgamma(shapeH, shapeH)

	# Effective population size and response
	alpha ~ dgamma(hetShape, hetShape/hetMean)
	effRep ~ dbeta(effRepHa, effRepHb)
	RRprop ~ dunif(0, 1)
	effProp <- pow(effRep,1-RRprop)
	repMean <- pow(effRep,RRprop)

	S[1] <- effProp*pop

	# kernel
	R0 ~ dgamma(Rshape, Rshape/Rmean)

	# genPos ~ dbeta(gpShape/(1-gpMean), gpShape/gpMean)
	genPos ~ dbeta(1, 1)
	# gpa ~ dgamma(gpShape/(1-gpMean), 1)
	# gpb ~ dgamma(gpShape/gpMean, 1)
	# genPos <- gpa/(gpa+gpb)

	genShape ~ dgamma(gsShape, gsShape/gsMean)

	# Pre-observation and kernel
	for (j in 1:lag){
		preker[j] <- pow(j, genShape/(1-genPos))*pow(lag-j+1, genShape/genPos)
		ker[j] <- R0*preker[j]/sum(preker)
		preInc[j] ~ dnegbin(preExp, 1)
	}

	for (j in 1:max){
		# _FOICHAIN_ is a magic word that's expanded in the autobug
		## foi is the expected number _reported_. 
		foi[j] <- FOICHAIN*pow(S[lag+j]/S[1], 1+alpha)+foieps

		## inc is really "shadow" incidence:
		##  the expected number reported _given_ the true incidence
		## preInc is calculated first to make sure inc is not too big
		## (cannot exceed S[lag+j]*repMean)

		preIncShape[j] <- (incShape*foi[j]/repMean)/(incShape+foi[j]/repMean)
		preInc[lag+j] ~ dgamma(preIncShape[j], preIncShape[j]/foi[j])

		# Observation process
		# obs[j] ~ dnegbin(inc[lag+j]*repShape/(inc[lag+j]+repShape), repShape)
		obsMean[j] ~ dgamma(repShape, repShape/inc[lag+j])
		obs[j] ~ dpois(obsMean[j])
	}

	for(j in 1:(lag+max)){
		S[j+1] <- foieps + S[j] - inc[j]/repMean

		inc[j] <- foieps + S[j]*repMean*
			(1 - pow(1+preInc[j]/(S[j]*repMean*kappa), -kappa)) 
	}

	gen <- inprod(lagvec, ker)/R0
}
