### gamma(shape, rate); rate = shape/mu
### beta: mean = a/(a+b) = shape = ab/(a+b)
### a = shape/mean; b=shape/(1-mean)

model{
	# Dispersion
	repMean ~ dbeta(repHa, repHb)
	repShape ~ dgamma(shapeH, shapeH)
	incShape ~  dgamma(shapeH, shapeH)

	# Effective population size and response
	effProp ~ dbeta(effPropHa, effPropHb)
	S[1] <- round(effProp*pop)

	# Pre-observation and kernel
	for (j in 1:lag){
		ker[j] ~ dgamma(kerShape, lag*kerShape/kerMean)
		inc[j] ~ dnegbin(preExp, 1)
		S[j+1] <- S[j] - inc[j]
	}

	for (j in 1:max){
		# _FOICHAIN_ is a magic word that's expanded in the autobug
		foi[j] <- (FOICHAIN + foieps)/S[1]
		incMean[j] <- 1 - pow(1+foi[j]/kappa, -kappa) 
		inca[j] ~ dgamma(incShape/(1-incMean[j]), 1)
		incb[j] ~ dgamma(incShape/incMean[j], 1)
		inc[lag+j] ~ dbin(inca[j]/(inca[j]+incb[j]), S[lag+j])

		S[lag+j+1] <- S[lag+j] - inc[lag+j]

		# Observation process
		# rep[j] ~ dbeta(repShape/repMean, repShape/(1-repMean))
		repa[j] ~ dgamma(repShape/(1-repMean), 1)
		repb[j] ~ dgamma(repShape/repMean, 1)
		obs[j] ~ dbin(repa[j]/(repa[j]+repb[j]), inc[lag+j])
	}

	R0 <- sum(ker)
	gtot <- inprod(lagvec, ker)
	gen <- (gtot/R0)
}
